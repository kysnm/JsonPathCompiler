plugins {
    id "com.jfrog.bintray" version "1.7"
    id "maven-publish"
    id "java"
    id "jacoco"
    // id "findbugs"
    id "idea"
}

repositories {
    mavenCentral()
    jcenter()
}

configurations {
    provided
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

dependencies {
    // The production code uses the SLF4J logging API at compile time
    compile 'org.slf4j:slf4j-api:1.7.21'
    compile 'net.minidev:json-smart:2.2.1'
    compile 'org.apache.commons:commons-lang3:3.4'

    testCompile 'junit:junit:4.+'
    testCompile 'org.assertj:assertj-core:2.1.0'
    testCompile 'org.slf4j:slf4j-simple:1.7.21'
}

allprojects {
    ext {
        snapshotVersion = true
        license = 'Apache-2.0'
        licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
        websiteUrl = 'https://github.com/kysnm/JsonPathCompiler'
        issueTrackerUrl = 'https://github.com/kysnm/JsonPathCompiler/issues'
        vcsUrl = 'https://github.com/kysnm/JsonPathCompiler.git'
        githubRepo = 'kysnm/JsonPathCompiler'
        displayName = null
        buildTimestamp = new Date().format('yyyy-MM-dd HH:mm:ss')
    }

    group = 'com.github.kysnm'
    description = 'Porting from https://github.com/jayway/JsonPath Cut out a PathCompiler.'
    version = '0.0.2' + (snapshotVersion ? "-SNAPSHOT" : "")

    if (JavaVersion.current().isJava8Compatible()) {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }


    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    javadoc {
        options.locale = 'en_US'
    }

    artifacts {
        archives jar
        archives sourcesJar
        archives javadocJar
    }

    publishing {
        publications {
            mavenPublication(MavenPublication) {
                from components.java
                groupId project.group
                artifactId project.name
                pom.withXml {
                    asNode().children().last() + {
                        resolveStrategy = DELEGATE_FIRST
                        name project.name
                        description project.description
                        url project.vcsUrl
                        scm {
                            url project.vcsUrl
                            connection "scm:git:$project.vcsUrl"
                        }
                        licenses {
                            license {
                                name project.license
                                url project.licenseUrl
                                distribution 'repo'
                            }
                        }
                        developers {
                            developer {
                                id project.group
                                name project.group
                            }
                        }
                    }
                }
            }
        }
    }

    bintray {
        user = project.hasProperty('bintray_user') ? bintray_user : ''
        key = project.hasProperty('bintray_apikey') ? bintray_apikey : ''

        pkg {
            repo = 'maven'
            name = project.name
            licenses = [project.license]
            websiteUrl = project.websiteUrl
            issueTrackerUrl = project.issueTrackerUrl
            vcsUrl = project.vcsUrl
            githubRepo = project.githubRepo
            version {
                name = project.version
                released = new Date()
                gpg {
                    sign = true
                    passphrase = project.hasProperty('bintray_gpg_password') ? bintray_gpg_password : ''
                }
            }
        }
        publications = ['mavenPublication']
        configurations = ['archives']

        dryRun = true
    }
}

task classpath(type: Copy, dependsOn: ["jar"]) {
    doFirst { file("classpath").deleteDir() }
    from(configurations.provided + files(jar.archivePath))
    into "classpath"
}
clean { delete "classpath" }
